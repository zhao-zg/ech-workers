buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.0'
    }
}

apply plugin: 'com.android.application'

def getVersionName() {
    // 从 tunnel/version.go 读取版本号
    def versionFile = file('../tunnel/version.go')
    if (versionFile.exists()) {
        def versionContent = versionFile.text
        def matcher = versionContent =~ /Version\s*=\s*"v?([0-9]+\.[0-9]+\.[0-9]+)"/
        if (matcher.find()) {
            return matcher.group(1)
        }
    }
    // 如果读取失败，返回默认版本
    return "1.0.8"
}

def getVersionCode() {
    def version = getVersionName()
    def parts = version.split('\\.')
    if (parts.length == 3) {
        return parts[0].toInteger() * 10000 + parts[1].toInteger() * 100 + parts[2].toInteger()
    }
    return 10008
}

android {
        namespace "com.ech.workers"
        compileSdkVersion 34
        ndkVersion "26.3.11579264"

        defaultConfig {
                applicationId "com.ech.workers"
                minSdkVersion 24
                targetSdkVersion 34
                versionCode getVersionCode()
                versionName getVersionName()
                
                // 添加 BuildConfig 字段以便在代码中访问版本号
                buildConfigField "String", "VERSION_NAME", "\"${getVersionName()}\""
                
                // archivesName 不在 defaultConfig 作用域，移至 base 扩展
                ndk {
                        abiFilters "armeabi-v7a", "arm64-v8a",'x86','x86_64'
                }
                externalNativeBuild {
                        ndkBuild {
                                arguments "APP_CFLAGS+=-DPKGNAME=com/ech/workers -ffile-prefix-map=${rootDir}=."
                                arguments "APP_LDFLAGS+=-Wl,--build-id=none"
                        }
                }
        }
        
        buildFeatures {
                buildConfig true
        }

signingConfigs {
    release {
        if (rootProject.file('store.properties').exists()) {
            def props = new Properties()
            props.load(new FileInputStream(rootProject.file('store.properties')))
            storeFile rootProject.file(props['storeFile'])
            storePassword props['storePassword']
            keyAlias props['keyAlias']
            keyPassword props['keyPassword']
        }
    }
}

        buildTypes {
                release {
                        minifyEnabled false
                        signingConfig signingConfigs.release
                }
                debug {
                        minifyEnabled false
                        // Debug builds use default debug signing
                }
        }

        splits {
                abi {
                        enable true
                        reset()
                        include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                        universalApk true
                }
        }

	def propsFile = rootProject.file('store.properties')
	def configName = 'release'

	if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
		def props = new Properties()
		props.load(new FileInputStream(propsFile))
		if (props!=null && props.containsKey('storeFile')) {
			android.signingConfigs[configName].storeFile = rootProject.file(props['storeFile'])
			android.signingConfigs[configName].storePassword = props['storePassword']
			android.signingConfigs[configName].keyAlias = props['keyAlias']
			android.signingConfigs[configName].keyPassword = props['keyPassword']
		}
	}

	externalNativeBuild {
		ndkBuild {
			path "src/main/jni/Android.mk"
		}
	}

	lintOptions {
		checkReleaseBuilds false
		// Or, if you prefer, you can continue to check for errors in release builds,
		// but continue the build even when errors are found:
		abortOnError false
	}

	dependenciesInfo {
		// Disables dependency metadata when building APKs.
		includeInApk = false
		// Disables dependency metadata when building Android App Bundles.
		includeInBundle = false
	}
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    // 设置APK输出文件名
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def abi = output.getFilter(com.android.build.OutputFile.ABI)
            // 确保使用正确的版本号
            def versionName = variant.versionName ?: getVersionName()
            def buildType = variant.buildType.name
            
            if (abi != null) {
                // 带架构的APK
                if (buildType == 'release') {
                    outputFileName = "com.ech.workers-${versionName}-${abi}-release.apk"
                } else {
                    outputFileName = "com.ech.workers-${versionName}-${abi}.apk"
                }
            } else {
                // 通用APK
                if (buildType == 'release') {
                    outputFileName = "com.ech.workers-${versionName}-universal-release.apk"
                } else {
                    outputFileName = "com.ech.workers-${versionName}-universal.apk"
                }
            }
        }
    }
}

repositories {
    google()
    mavenCentral()
    flatDir {
        dirs '.'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation files('ech-workers.aar')
}
